PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix om:  <http://www.ontology-of-units-of-measure.org/resource/om-2/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

prefix ts: <https://www.theworldavatar.com/kg/ontotimeseries/>
prefix ontobms: <https://www.theworldavatar.com/kg/ontobms/>
prefix saref: <https://saref.etsi.org/core/>
prefix ontodevice: <https://www.theworldavatar.com/kg/ontodevice/>
prefix : <https://www.theworldavatar.com/kg/ontobms/>
SELECT distinct ?Measurement ?Name ?Unit WHERE {
  {
    [IRI] saref:consistsOf / ontodevice:measures ?sashOp.
    ?sashOp om:hasValue ?Measurement.
   	?Measurement om:hasUnit / om:symbol ?Unit.
    BIND (strbefore(replace(str(?sashOp), str(ontobms:), ""), "_") as ?Name).
  } union {
    [IRI] saref:consistsOf / ontodevice:observes ?Measurement.
    BIND ("Status" as ?Name).
    BIND ("-" as ?Unit).
  } union {
    [IRI] saref:hasState ?Measurement.
    ?Measurement rdf:type ontodevice:OccupiedState.
    BIND ("Occupancy" as ?Name).
    BIND ("-" as ?Unit).
  } union {
    [IRI] saref:consistsOf / ontodevice:sendsSignalTo / saref:consistsOf / ontodevice:measures / om:hasValue ?Measurement.
    ?Measurement om:hasUnit / om:symbol ?Unit.
    BIND ("AirFlow" as ?Name).
  } union {
    [IRI] saref:consistsOf / ontodevice:sendsSignalTo / saref:consistsOf / ontodevice:observes ?Measurement.
    BIND ("DamperState" as ?Name).
    BIND ("-" as ?Unit).
  }
}

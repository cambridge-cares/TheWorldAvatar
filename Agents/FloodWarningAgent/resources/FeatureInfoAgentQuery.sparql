prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix flood: <https://www.theworldavatar.com/kg/ontoflood/>
prefix rt:    <http://environment.data.gov.uk/flood-monitoring/def/core/>
prefix om:    <http://www.ontology-of-units-of-measure.org/resource/om-2/>

SELECT ?label ?value ?unit
WHERE {
  [IRI] ^flood:hasArealExtent/^flood:hasLocation ?area . 
  ?area rt:currentWarning ?warning . 
  ?warning flood:warnsAbout ?flood .

  # Flood Alert or Warning properties
  { BIND ("Label" AS ?label )
    ?warning rdf:label ?value
  } UNION {
    BIND ("Message" AS ?label )
    ?warning rt:message ?value
  } UNION {
    BIND ("Initially raised" AS ?label )
    ?warning rt:timeRaised ?value
  } UNION {
    BIND ("Message last changed" AS ?label )
    ?warning rt:timeMessageChanged ?value
  } UNION {
    BIND ("Severity last changed" AS ?label )
    ?warning rt:timeSeverityChanged ?value
  } UNION {
    BIND ("Severity" AS ?label )
    ?warning flood:hasSeverity/rdfs:label ?value
  }
  
  # Flood Area properties
  UNION { 
    BIND ("Flood area" AS ?label )
    ?area rdfs:label ?value .
  } UNION { 
    BIND ("Attached water body" AS ?label )
    ?area flood:attachedWaterBody/rdfs:label ?value .
  }

  # Flood Event properties
  UNION { 
    BIND ("Affected population" AS ?label )
    ?flood flood:affects ?population . 
    ?population rdf:type flood:Population ;
                flood:hasTotalCount ?value . 
    BIND ("people" as ?unit)
  } UNION { 
    BIND ("Affected buildings" AS ?label )
    ?flood flood:affects ?buildings . 
    ?buildings rdf:type flood:Building ;
               flood:hasTotalCount ?value . 
    BIND ("number" as ?unit)
  } UNION { 
    BIND ("Property value at risk" AS ?label )
    ?flood flood:affects ?buildings . 
    ?buildings rdf:type flood:Building ;
               flood:hasTotalMonetaryValue/om:hasValue ?measure . 
    ?measure om:hasNumericalValue ?value ; 
             om:hasUnit/om:symbol ?unit .
  } 
}
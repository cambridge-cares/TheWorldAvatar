prefix rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf:       <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix obe:       <https://www.theworldavatar.com/kg/ontobuiltenv/>
prefix dabgeo:    <http://www.purl.org/oema/infrastructure/>
prefix icontact:  <http://ontology.eil.utoronto.ca/icontact.owl#>
prefix time:      <https://www.w3.org/2006/time#>
prefix om:        <http://www.ontology-of-units-of-measure.org/resource/om-2/>
prefix lrppi:     <http://landregistry.data.gov.uk/def/ppi/>

SELECT ?label ?value ?unit
WHERE {
  # Address information
  { 
    BIND ("Administrative District" AS ?label )
    <PROPERTY_IRI> obe:locatedIn/rdfs:label ?value .
  } UNION { 
    BIND ("Postcode" AS ?label )
    <PROPERTY_IRI> obe:hasAddress/obe:hasPostalCode/rdfs:label ?value .
  } UNION { 
    BIND ("Building" AS ?label )
    <PROPERTY_IRI> obe:hasAddress/icontact:hasBuilding ?value . 
  } UNION { 
    BIND ("Address" AS ?label )
    <PROPERTY_IRI> obe:hasAddress ?address .
    OPTIONAL {?address icontact:hasStreet ?street . }
    BIND( IF( EXISTS{ ?address icontact:hasStreet ?street }, str(?street), "") AS ?street ) 
    OPTIONAL {?address icontact:hasStreetNumber ?nr . } 
    BIND( IF( EXISTS{ ?address icontact:hasStreetNumber ?nr }, str(?nr), "") AS ?nr ) 
    OPTIONAL {?address obe:hasUnitName ?unit_name . }
    BIND( IF( EXISTS{ ?address obe:hasUnitName ?unit_name }, concat(", ", str(?unit_name)), "") AS ?unit_name ) 
    BIND (concat(?street, " ", ?nr, " ", ?unit_name) as ?value)
  }
  # General building information
  UNION
  { BIND ("Type" AS ?label )
    <PROPERTY_IRI> rdf:type ?value_ini .
    BIND (replace(replace(str(?value_ini), str(dabgeo:), ''), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Name" AS ?label )
    <PROPERTY_IRI> rdfs:label ?value .
  } UNION { 
    BIND ("Identifier" AS ?label )
    BIND ("(i.e. UPRN where available)" AS ?unit )
    <PROPERTY_IRI> obe:hasIdentifier ?value .
  } UNION { 
    BIND ("EPC rating" AS ?label )
    <PROPERTY_IRI> obe:hasEnergyRating ?value .
  } UNION { 
    BIND ("Latest EPC" AS ?label )
    BIND ("(i.e. individual lodgement identifier)" AS ?unit )
    <PROPERTY_IRI> obe:hasLatestEPC ?value .
  } UNION { 
    BIND ("Property type" AS ?label )
    <PROPERTY_IRI> obe:hasPropertyType/rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Built form" AS ?label )
    <PROPERTY_IRI> obe:hasBuiltForm/rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Usage category" AS ?label )
    <PROPERTY_IRI> obe:hasUsageCategory/rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
  }
  # Numerical building information
  UNION { 
    BIND ("Number of rooms" AS ?label )
    <PROPERTY_IRI> obe:hasNumberOfHabitableRooms ?value .
  } UNION { 
    BIND ("Floor area" AS ?label )
    <PROPERTY_IRI> obe:hasTotalFloorArea/om:hasValue ?measure .
    ?measure om:hasNumericalValue ?value ;
             om:hasUnit/om:symbol ?unit
  } UNION { 
    BIND ("Ground elevation (above MSL)" AS ?label )
    <PROPERTY_IRI> obe:hasGroundElevation/om:hasValue ?measure .
    ?measure om:hasNumericalValue ?value ;
             om:hasUnit/om:symbol ?unit
  } UNION { 
    BIND ("Built before" AS ?label )
    <PROPERTY_IRI> obe:hasConstructionDate/time:hasEnd/time:inXSDDateTimeStamp ?value_ini .
    BIND (STRBEFORE(str(?value_ini), '-') as ?value)
  }
  # Construction information
  UNION { 
    BIND ("Wall description" AS ?label )
    <PROPERTY_IRI> obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Wall ;
          rdfs:comment ?value .
  } UNION { 
    BIND ("Roof description" AS ?label )
    <PROPERTY_IRI> obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Roof ;
          rdfs:comment ?value .
  } UNION { 
    BIND ("Floor description" AS ?label )
    <PROPERTY_IRI> obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Floor ;
          rdfs:comment ?value .
  } UNION { 
    BIND ("Windows description" AS ?label )
    <PROPERTY_IRI> obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Windows ;
          rdfs:comment ?value .
  } 
  # Value estimations
  UNION { 
    BIND ("Last sold" AS ?label )
    <PROPERTY_IRI> obe:hasLatestTransactionRecord/lrppi:transactionDate ?value
  } UNION { 
    BIND ("Last sold for" AS ?label )
    <PROPERTY_IRI> obe:hasLatestTransactionRecord/lrppi:pricePaid ?value .
    BIND ("GBP" AS ?unit )
  }
}

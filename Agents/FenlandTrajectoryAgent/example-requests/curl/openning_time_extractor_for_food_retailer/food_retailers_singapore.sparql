PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ontofhrs: <http://www.theworldavatar.com/ontology/OntoFHRS/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX om: <http://www.ontology-of-units-of-measure.org/resource/om-2/>
PREFIX osmkey: <https://www.openstreetmap.org/wiki/Key:>
PREFIX ogc: <http://www.opengis.net/rdf#>

CONSTRUCT {
  ?asset a ?my_type ;
         geo:hasGeometry ?defaultGeometry ;
         rdfs:label ?name ;
         ontofhrs:openingTimes ?openingHours .
}
WHERE {
  ?asset ?osm_pred ?osm_type ;
         geo:hasGeometry ?defaultGeometry .
  ?region osmkey:name "Singapore" ;
          ogc:sfContains ?asset .
  ?defaultGeometry geo:asWKT ?wkt .

  OPTIONAL { ?asset osmkey:name ?name . }
  OPTIONAL { ?asset osmkey:opening_hours ?openingHours . }

  VALUES (?osm_pred ?osm_type ?my_type) {
    (osmkey:amenity "restaurant" ontofhrs:Restaurant)
    (osmkey:amenity "fast_food" ontofhrs:FastFood)
    (osmkey:amenity "cafe" ontofhrs:Cafe)
    (osmkey:shop "food" ontofhrs:FoodShop)
    (osmkey:shop "supermarket" ontofhrs:Supermarket)
    (osmkey:shop "convenience" ontofhrs:ConvenienceStore)
  }
}

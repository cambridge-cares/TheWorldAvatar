PREFIX sh:             <http://www.w3.org/ns/shacl#>
PREFIX fibo-fnd-dt-fd: <https://spec.edmcouncil.org/fibo/ontology/FND/DatesAndTimes/FinancialDates/>

SELECT DISTINCT ?clazz ?name ?nodename ?subject ?isparent ?isoptional ?isnumber ?multipath ?multipath_proppath
WHERE{
  ?shape rdf:type sh:NodeShape ;
         sh:targetClass ?clazz ;
         sh:targetClass [target] ;
         sh:property[shape] ?property .
  ?property sh:name ?name ;
            sh:path ?predicatepath .
  OPTIONAL{
    ?property sh:hasValue ?parentSubjectValue .
  }
  OPTIONAL{
    ?property sh:qualifiedValueShape ?parent  .
  }
  OPTIONAL{
    ?property sh:minCount ?minCount  .
  }
  # Verify numerical value
  OPTIONAL{
    ?property sh:datatype ?datatype.
  }
  # Predicate paths may be stored in a list or not, and use of ? helps to extract the related property
  OPTIONAL{
    ?predicatepath rdf:first?[path] ?multipath_initial .
    # If the property path is a nested blank node, extract both the shacl path restriction eg sh:inversePath and path name
    OPTIONAL{
      ?multipath_initial ?multipath_proppath ?multipath_listitem .
      FILTER STRSTARTS(STR(?multipath_proppath), STR(sh:))
    }
    # Extract the required path variable
    BIND(IF(?multipath_proppath != "" && isBlank(?multipath_initial), ?multipath_listitem, ?multipath_initial) AS ?multipath)
    FILTER(BOUND(?multipath))
  }

  # If there is a parent indicator, the property should be a parent
  BIND(BOUND(?parent) AS ?isparent)
  # Retrieve sub target value first; Else, attempt to retrieve from parent or return null 
  BIND(IF(BOUND(?parentSubjectValue), ?parentSubjectValue, ?null) AS ?subject)
  # Check if the path has a min count property to determine if it is optional
  BIND(!BOUND(?minCount) AS ?isoptional)
  # Verify if the data type is a number (integer/decimal)
  BIND(BOUND(?datatype) && (?datatype = xsd:integer || ?datatype =xsd:decimal) AS ?isnumber)
  
  # Retrieve results for non-regular schedules
  OPTIONAL{?property sh:class ?class}
  FILTER(!BOUND(?class) || ?class != fibo-fnd-dt-fd:RegularSchedule)

  # Only retrieve results if there is either a multipath
  FILTER(BOUND(?multipath))
}
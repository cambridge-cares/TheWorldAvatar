PREFIX sh:             <http://www.w3.org/ns/shacl#>
PREFIX fibo-fnd-dt-fd: <https://spec.edmcouncil.org/fibo/ontology/FND/DatesAndTimes/FinancialDates/>

SELECT DISTINCT ?clazz ?instance_clazz ?name ?nodegroup ?order ?subject ?isoptional ?isclass
 ?multipath ?multipath_proppath ?name_multipath ?name_multipath_proppath
WHERE {
  ?shape rdf:type sh:NodeShape ;
         sh:targetClass ?clazz ;
         sh:targetClass [target] ;
         sh:property[shape] ?property .
  ?property sh:name ?name ;
            sh:path ?predicatepath ;
            sh:order ?order .
  OPTIONAL{
    ?property sh:hasValue ?parentSubjectValue .
  }
  OPTIONAL{
    ?property sh:minCount ?minCount  .
  }
  # Verify numerical value
  OPTIONAL{
    ?property sh:datatype ?datatype.
  }
  # Optional query for name property label for the property
  OPTIONAL{
    ?property (sh:class|sh:in)/^sh:targetClass/sh:property ?name_property .
    ?name_property sh:name ?name_field;
                   sh:path ?name_predpath.
    FILTER(?name_field = "name")
    
    OPTIONAL{
      ?name_predpath rdf:first?[path] ?name_multipath_initial .
      # If the property path is a nested blank node, extract both the shacl path restriction eg sh:inversePath and path name
      OPTIONAL{
        ?name_multipath_initial ?name_multipath_proppath ?name_multipath_listitem .
        FILTER STRSTARTS(STR(?name_multipath_proppath), STR(sh:))
      }
      # Extract the required path variable
      BIND(IF(?name_multipath_proppath != "" && isBlank(?name_multipath_initial), ?name_multipath_listitem, ?name_multipath_initial) AS ?name_multipath)
      FILTER(BOUND(?name_multipath))
    }
   }

  # Predicate paths may be stored in a list or not, and use of ? helps to extract the related property
   OPTIONAL{
    ?predicatepath rdf:first?[path] ?multipath_initial .
    # If the property path is a nested blank node, extract both the shacl path restriction eg sh:inversePath and path name
    OPTIONAL{
      ?multipath_initial ?multipath_proppath ?multipath_listitem .
      FILTER STRSTARTS(STR(?multipath_proppath), STR(sh:))
    }
    # Extract the required path variable
    BIND (IF(?multipath_proppath != "" && isBlank(?multipath_initial), ?multipath_listitem, ?multipath_initial) AS ?multipath)
    FILTER (BOUND(?multipath))
  }

  # Retrieve parent target value if available or return null 
  BIND(IF(BOUND(?parentSubjectValue), ?parentSubjectValue, ?null) AS ?subject)
  # Check if the path has a min count property to determine if it is optional
  BIND(BOUND(?minCount) && (?minCount=0) AS ?isoptional)
  # Verify if the data type is a number (integer/decimal)
  BIND(BOUND(?datatype) && (?datatype = xsd:integer || ?datatype =xsd:decimal) AS ?isnumber)

  # Retrieve results for non-regular schedules
  OPTIONAL{?property sh:class ?instance_clazz}
  FILTER (!BOUND(?instance_clazz) || ?instance_clazz != fibo-fnd-dt-fd:RegularSchedule)

  # Check if this is a TBox class concept that requires human readable labels
  OPTIONAL{?property sh:in ?classconcept}
  BIND(BOUND(?classconcept) AS ?isclass)

  # Only retrieve results if there is either a multipath, multisubpath, or name_multipath
  FILTER(BOUND(?multipath) || BOUND(?multisubpath) || BOUND(?name_multipath))
}
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix om:  <http://www.ontology-of-units-of-measure.org/resource/om-2/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

prefix ts: <https://www.theworldavatar.com/kg/ontotimeseries/>
prefix ontobms: <https://www.theworldavatar.com/kg/ontobms/>
prefix saref: <https://saref.etsi.org/core/>
prefix ontodevice: <https://www.theworldavatar.com/kg/ontodevice/>
prefix : <https://www.theworldavatar.com/kg/ontobms/>
SELECT distinct ?Measurement ?Name ?Unit WHERE {
  {
    [IRI] saref:consistsOf / ontodevice:measures ?a.
    ?a om:hasValue ?Measurement.
    ?Measurement om:hasUnit / om:symbol ?Unit.
    BIND (strbefore(replace(str(?a), str(ontobms:), ""), "_") as ?Name).
  } union {
    [IRI] saref:consistsOf / ontodevice:observes ?Measurement.
    BIND ("DamperState" as ?Name).
    BIND ("-" as ?Unit).
  } union {
    [IRI] saref:consistsOf / saref:consistsOf / saref:hasState ?Measurement.
    BIND ("Hot Water Valve Status" as ?Name).
    BIND ("-" as ?Unit).
  } union {
    [IRI] saref:consistsOf / saref:consistsOf / ontodevice:hasControlParameter / om:hasValue ?Measurement.
    ?Measurement om:hasUnit / om:symbol ?Unit.
    BIND ("Hot Water Valve Control" as ?Name).
  } union {
    [IRI] saref:consistsOf / saref:hasState ?Measurement.
    BIND (concat("Heater ", strafter(str(?Measurement), "Heater_")) as ?Name).
    BIND ("-" as ?Unit).
  } union {
    [IRI] saref:consistsOf / ontodevice:hasControlParameter / om:hasValue ?Measurement.
    ?Measurement om:hasUnit / om:symbol ?Unit.
    BIND ("Heater Control" as ?Name).
  } union {
  [IRI] ontodevice:hasSetpoint / ontodevice:hasQuantity / om:hasValue ?Measurement.
  ?Measurement om:hasUnit / om:symbol ?Unit.
  BIND ("AirFlow Setpoint" as ?Name).
  }
}

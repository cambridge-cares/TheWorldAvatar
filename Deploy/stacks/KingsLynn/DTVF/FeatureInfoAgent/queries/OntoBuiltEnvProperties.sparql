prefix rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf:       <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix obe:       <https://www.theworldavatar.com/kg/ontobuiltenv/>
prefix dabgeo:    <http://www.purl.org/oema/infrastructure/>
prefix icontact:  <http://ontology.eil.utoronto.ca/icontact.owl#>
prefix time:      <https://www.w3.org/2006/time#>
prefix om:        <http://www.ontology-of-units-of-measure.org/resource/om-2/>
prefix lrppi:     <http://landregistry.data.gov.uk/def/ppi/>

SELECT ?label ?value ?unit
WHERE {
  # Address information
  { 
    BIND ("Administrative District" AS ?label )
    [IRI] obe:locatedIn/rdfs:label ?value .
  } UNION { 
    BIND ("Postcode" AS ?label )
    [IRI] obe:hasAddress/obe:hasPostalCode/rdfs:label ?value .
  } UNION { 
    BIND ("Building name" AS ?label )
    [IRI] obe:hasAddress/icontact:hasBuilding ?value . 
  } UNION { 
    BIND ("Address" AS ?label )
    [IRI] obe:hasAddress ?address .
    OPTIONAL {?address icontact:hasStreet ?street_ini . }
    BIND( IF( EXISTS{ ?address icontact:hasStreet ?street_ini }, str(?street_ini), "") AS ?street ) 
    OPTIONAL {?address icontact:hasStreetNumber ?nr_ini . } 
    BIND( IF( EXISTS{ ?address icontact:hasStreetNumber ?nr_ini }, str(?nr_ini), "") AS ?nr ) 
    OPTIONAL {?address obe:hasUnitName ?unit_name_ini . }
    BIND( IF( EXISTS{ ?address obe:hasUnitName ?unit_name_ini }, concat(", ", str(?unit_name_ini)), "") AS ?unit_name ) 
    BIND (concat(?street, " ", ?nr, " ", ?unit_name) as ?value)
  }
  # General building information
  UNION
  { BIND ("Type" AS ?label )
    [IRI] rdf:type ?value_ini .
    BIND (replace(replace(str(?value_ini), str(dabgeo:), ''), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Identifier" AS ?label )
    BIND ("UPRN where available" AS ?unit )
    [IRI] obe:hasIdentifier ?value .
  } UNION { 
    BIND ("EPC rating" AS ?label )
    [IRI] obe:hasEnergyRating ?value .
  } UNION { 
    BIND ("Latest EPC" AS ?label )
    BIND ("(i.e. individual lodgement identifier)" AS ?unit )
    [IRI] obe:hasLatestEPC ?value .
  } UNION { 
    BIND ("Property type" AS ?label )
    [IRI] obe:hasPropertyType/rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Built form" AS ?label )
    [IRI] obe:hasBuiltForm/rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
  } UNION { 
    BIND ("Property usage" AS ?label )
    [IRI] obe:hasPropertyUsage ?usage .
    ?usage rdf:type ?value_ini .
    OPTIONAL { ?usage obe:hasUsageShare ?unit_ini . }
    BIND (replace(str(?value_ini), str(obe:), '') as ?value)
    BIND (concat(str(?unit_ini*100), ' %') as ?unit)
  }
  # Numerical building information
  UNION { 
    BIND ("Number of rooms" AS ?label )
    [IRI] obe:hasNumberOfHabitableRooms ?value .
  } UNION { 
    BIND ("Floor area" AS ?label )
    [IRI] obe:hasTotalFloorArea/om:hasValue ?measure .
    ?measure om:hasNumericalValue ?value ;
             om:hasUnit/om:symbol ?unit
  } UNION { 
    BIND ("Ground elevation (above MSL)" AS ?label )
    [IRI] obe:hasGroundElevation/om:hasValue ?measure .
    ?measure om:hasNumericalValue ?value ;
             om:hasUnit/om:symbol ?unit
  } UNION { 
    BIND ("Built before" AS ?label )
    [IRI] obe:hasConstructionDate/time:hasEnd/time:inXSDDateTimeStamp ?value_ini .
    BIND (STRBEFORE(str(?value_ini), '-') as ?value)
  }
  # Construction information
  UNION { 
    BIND ("Wall description" AS ?label )
    [IRI] obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Wall ;
          rdfs:comment ?value_ini .
    BIND (replace(str(?value_ini), 'W/m\\?K', 'W/m²K') as ?value)
  } UNION { 
    BIND ("Roof description" AS ?label )
    [IRI] obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Roof ;
          rdfs:comment ?value_ini .
    BIND (replace(str(?value_ini), 'W/m\\?K', 'W/m²K') as ?value)
  } UNION { 
    BIND ("Floor description" AS ?label )
    [IRI] obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Floor ;
          rdfs:comment ?value_ini .
    BIND (replace(str(?value_ini), 'W/m\\?K', 'W/m²K') as ?value)
  } UNION { 
    BIND ("Windows description" AS ?label )
    [IRI] obe:hasConstructionComponent ?comp .
    ?comp rdf:type obe:Windows ;
          rdfs:comment ?value_ini .
    BIND (replace(str(?value_ini), 'W/m\\?K', 'W/m²K') as ?value)
  } 
  # Value estimations
  UNION { 
    BIND ("Last sold" AS ?label )
    [IRI] obe:hasLatestTransactionRecord/lrppi:transactionDate ?value
  } UNION { 
    BIND ("Last sold for" AS ?label )
    [IRI] obe:hasLatestTransactionRecord/lrppi:pricePaid ?value_ini .
    BIND (strlen(str(?value_ini)) as ?l)
	  BIND (replace(replace(str(?value_ini), '\\,' ,''), '\\.', '') as ?value_interim)
    BIND (concat(substr(?value_interim, 0, ?l-2), ',', substr(?value_interim, ?l-2, 3)) as ?value)
    BIND ("£" AS ?unit )
  }
}

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix ems:  <https://www.theworldavatar.com/kg/ontoems/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rt:   <http://environment.data.gov.uk/flood-monitoring/def/core/>
prefix om:   <http://www.ontology-of-units-of-measure.org/resource/om-2/>

SELECT ?Property ?Value ?Unit
WHERE {
  { BIND ("Type" AS ?Property )
    [IRI] rdf:type ?value_ini .
    BIND (replace(str(?value_ini), str(ems:), '') as ?Value)
  } UNION { 
    BIND ("Name" AS ?Property )
    [IRI] rdfs:label ?Value .
  } UNION { 
    BIND ("Data source" AS ?Property )
    [IRI] ems:dataSource ?Value .
  } UNION { 
    # Add OR property path ("|") to allow for different name relations between MetOffice/AirQuality stations and WaterLevelStations (i.e. EA stations)
    BIND ("Identifier" AS ?Property )
    [IRI] ems:hasIdentifier|skos:notation ?Value .
  } UNION { 
    BIND ("Elevation" AS ?Property )
    BIND ("m" AS ?Unit )
    [IRI] ems:hasObservationElevation ?Value .
  }
  # Add additional information for EA river level stations (if available)
  UNION {
    BIND ("River name" AS ?Property )
    [IRI] rt:riverName ?Value .
  } UNION {
    BIND ("Catchment name" AS ?Property )
    [IRI] rt:catchmentName ?Value .
  } UNION {
    BIND ("Opening data" AS ?Property )
    [IRI] rt:dateOpened ?Value .
  } UNION {
    BIND ("Upstream station" AS ?Property )
    [IRI] ^ems:hasDownstreamStation ?Value .
  } UNION {
    BIND ("Downstream station" AS ?Property )
    [IRI] ems:hasDownstreamStation ?Value .
  }
  # Add Trend and Range for WaterLevelReportingStations
  # (workaround as additional info on time series panel not shown)
  UNION {
    BIND ("Current waterlevel range" AS ?Property )
    [IRI] ems:reports ?Measurement .
    ?Measurement rdf:type ems:WaterLevel ; 
                 om:hasValue/ems:hasCurrentRange ?range_ini . 
    BIND (replace(replace(str(?range_ini), str(ems:), ''), 'Range', '') as ?Value)
  } UNION {
    BIND ("Current waterlevel trend" AS ?Property )
    [IRI] ems:reports ?Measurement .
    ?Measurement rdf:type ems:WaterLevel ; 
                 om:hasValue/ems:hasCurrentTrend ?trend_ini . 
    BIND (replace(replace(str(?trend_ini), str(ems:), ''), 'Trend', '') as ?Value)
  }
}
